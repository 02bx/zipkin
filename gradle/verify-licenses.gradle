/**
 * Gradle plugin used to verify that all dependencies of a project use allowed licenses.
 * Usage:
 *   apply from: "${rootDir}/gradle/verify-licenses.gradle"
 *
 * The list of allowed licenses can be modified via `licenseWhiteList` from project definitions.
 */

ext.licenseWhiteList = [
		'Public Domain',
		'MIT License',
		'Apache License, Version 2.0',
		'BSD License',
		'BSD 3-Clause',
		'BSD-Style',
		'GNU Lesser General Public License',
		'Common Public License Version 1.0',
		'LGPL-2.1',
		'CC0 1.0 Universal',
		'CDDL v1.0',
		'Eclipse Public License - v 1.0'
]

// Generate license report
downloadLicenses {
	includeProjectDependencies = true
	dependencyConfiguration = 'compile'

	ext.apache = license('Apache License, Version 2.0', 'http://opensource.org/licenses/Apache-2.0')
	ext.mit = license('MIT License', 'http://www.opensource.org/licenses/mit-license.php')
	ext.bsd = license('BSD License', 'http://www.opensource.org/licenses/bsd-license.php')
	ext.bsd3Clause = license('BSD 3-Clause', 'http://opensource.org/licenses/BSD-3-Clause')
	ext.cddl = license('CDDL v1.0', 'https://glassfish.java.net/public/CDDLv1.0.html')
	ext.epl = license('Eclipse Public License - v 1.0', 'https://www.eclipse.org/legal/epl-v10.html')

	aliases = [
			(apache) : [
					'The Apache Software License, Version 2.0', 'Apache License Version 2.0', 'Apache License, Version 2.0',
					'Apache 2', 'Apache 2.0', 'Apache License 2.0', 'Apache-2.0',
					license('Apache License', 'http://www.apache.org/licenses/LICENSE-2.0'),
					license('Apache Software Licenses', 'http://www.apache.org/licenses/LICENSE-2.0.txt'),
					license('Apache', 'http://www.opensource.org/licenses/Apache-2.0')
			],
			(mit) : ['The MIT License'],
			(bsd) : ['BSD', 'Berkeley Software Distribution (BSD) License',
					 license('New BSD License', 'http://www.opensource.org/licenses/bsd-license.php')
			],
			(bsd3Clause) : [
					license('BSD 3-clause', 'http://opensource.org/licenses/BSD-3-Clause'),
					license('BSD 3-Clause', 'http://www.scala-lang.org/license.html'),
					license('BSD 3-Clause', 'http://asm.ow2.org/license.html')
			]
	]

	licenses = [
			(group('io.zipkin')): apache,
			'javax.servlet:servlet-api:2.5': cddl,   // see any .java file in the source .jar at http://search.maven.org/#artifactdetails|javax.servlet|servlet-api|2.5|jar
                        // jnr-posix is used by cassandra-driver-core until 3.0 https://github.com/openzipkin/zipkin/issues/952
                        'com.github.jnr:jnr-posix:3.0.29': epl, // recently changed: see https://github.com/jnr/jnr-posix/blob/jnr-posix-3.0.29/LICENSE.txt
			// Even though it'd be nice to just say "org.apache.*", that's not currently supported
			// see https://github.com/hierynomus/license-gradle-plugin/blob/067175d2d0ce5213c9cbab9968856b0feb7ed8d5/src/main/groovy/nl/javadude/gradle/plugins/license/LicenseResolver.groovy#L56-L59
			// and https://github.com/hierynomus/license-gradle-plugin/issues/100
			(group('org.apache.httpcomponents')): apache,
			(group('org.apache.zookeeper')): apache,
			(group('asm')): license('BSD 3-Clause', 'http://asm.ow2.org/license.html'),
			// Same goes for these
			(group('com.twitter.common')): apache,
			(group('com.twitter.common.zookeeper')): apache,
	]
}

// Verify that all dependency licenses are ones we like
task verifyLicenses {
	description "Verify that all dependencies use white-listed licenses."
	dependsOn ':downloadLicenses'

	doLast {
		def xml = new XmlParser().parse('build/reports/license/license-dependency.xml')
		def fail = false
		xml.each { license ->
			if (!licenseWhiteList.contains(license.@name)) {
				def depStrings = []
				license.dependency.each { depStrings << it.text() }
				logger.error(
						"License \"${license.@name}\" is not on the list of allowed licenses. " +\
                        "The dependencies using it: ${depStrings}")
				fail = true
			}
		}
		if (fail) {
			throw new GradleException("License verification failed.")
		}
	}
}
check.dependsOn verifyLicenses
